#
# Sorin module modified
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/nrGV6pg.png
#

#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies.
pmodload 'helper'

function prompt_stefano_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_stefano_pwd="$MATCH"
    unset MATCH
  else
    _prompt_stefano_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_stefano_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  git-info

  # Check for activated virtualenv
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Format PWD.
  prompt_stefano_pwd

  # Define prompts.
  RPROMPT='${editor_info[overwrite]}%(?:: %F{1}âŽ%f)${VIM:+" %B%F{6}V%f%b"}'
  RPROMPT+='${git_info:+${(e)git_info[status]}}$python_info[virtualenv]'
}

function prompt_stefano_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_stefano_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{15}...%f%b'
  # zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{1}â¯%F{3}â¯%F{2}â¯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' â¯%F{3}'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}â™º%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{2}â®%F{3}â®%F{1}â®%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{15}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%F{36}âœš%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}â¬†%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{13}â¬‡%f%%b'
  zstyle ':prezto:module:git:info:branch' format ' %%F{45}%b%f%%b'
  zstyle ':prezto:module:git:info:commit' format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}âœ–%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{220}âœ±%f%%b'
  zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}âžœ%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{6}âœ­%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}â•%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{15}â—¼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'status' '$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u'

  # Set Python Virtualenv parameters.
  zstyle ':prezto:module:python:info:virtualenv' format ' ðŸ%v'

  # Define prompts.
  # PROMPT='${SSH_TTY:+"%F{9}%n%f%F{15}@%f%F{3}%m%f "}%F{220}${_prompt_stefano_pwd}%(!. %B%F{1}#%f%b.)${editor_info[keymap]} '
  
  # Define prompts.
  PROMPT='${SSH_TTY:+"%F{9}%n%f%F{15}@%f%F{3}%m%f "}%F{220}${_prompt_stefano_pwd}%(!. %B%F{1}#%f%b.)${editor_info[keymap]} '
  RPROMPT=''
  SPROMPT='zsh: ninja-correct %F{1}%R%f to %F{36}%r%f [nyae]? '
}

function prompt_stefano_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2>/dev/null
  prompt_preview_theme 'stefano'
}

prompt_stefano_setup "$@"

#
# Completion
#
